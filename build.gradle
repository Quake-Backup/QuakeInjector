plugins {
    id 'java'
    id 'eclipse'
    id 'application'
//    id 'com.palantir.graal' version '0.9.0'
    id "com.github.spotbugs" version "4.7.1"
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

version = "git describe --tags".execute().text.trim()
repositories {
    mavenCentral()
}

dependencies {
    implementation 'edu.stanford.ejalbert:BrowserLauncher2:1.3'
}

application {
    mainClass.set('de.haukerehfeld.quakeinjector.QuakeInjector')
}

jar {
    manifest {
        attributes["Main-Class"] = application.mainClass.get()
    }
}

//graal {
//    javaVersion "11"
//    graalVersion "21.1.0"
//    outputName "quake-injector"
//    mainClass application.mainClassName
//    option "--no-fallback"
//    option "--verbose"
//}

task readCommit(type: WriteProperties) {
    outputFile = file("${buildDir}/build-info.properties")
    comment = "Revision name"
    property("quake-injector.build-commit", version)
}

launch4j {
    mainClassName = application.mainClass.get()
    icon = "${projectDir}/src/main/resources/Inject2.ico"
    bundledJrePath "./runtime"
    bundledJre64Bit false
}

processResources {
    from(readCommit)
}

task downloadJre {
    def targetDir = "$buildDir/jre"
    def f = new File("$buildDir/jre.zip")
    f.parentFile.mkdir()
    if (!f.exists()) {
        new URL('https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.10%2B7/OpenJDK17U-jdk_x86-32_windows_hotspot_17.0.10_7.zip').withInputStream {i -> f.withOutputStream {it << i}}
    }

    copy {
        from zipTree("$buildDir/jre.zip")
        into(targetDir)
        fileMode 0777
        dirMode 0777
    }
}

task setupWinDist(type: Copy) {
    dependsOn("createExe", "downloadJre")
    group "distribution"
    def targetDir = "$buildDir/winDist"
    mkdir(targetDir)
    File jreDir
    doLast {
        copy {
            file("$buildDir/jre").eachDir {
                if (it.name.contains("jdk")) {
                    jreDir = it
                }
            }
            if (jreDir) {
                from(jreDir)
                into("$targetDir/runtime")
            }
        }
    }
    from("$buildDir/launch4j")
    into(targetDir)
}

task winDist(type: Zip) {
    dependsOn "setupWinDist"
    group "distribution"
    def distDir = "$buildDir/winDist"
    from "$distDir"
    archiveFileName = "QuakeInjector.exe-${project.version}.zip"
    exclude("*-jre")

}
test {
    useJUnitPlatform()
}
